load("@buildifier_prebuilt//:rules.bzl", "buildifier")
load("@gazelle//:def.bzl", "gazelle")
load("@rules_go//go:def.bzl", "go_binary", "go_library")
load("@rules_pkg//pkg:tar.bzl", "pkg_tar")
load("@rules_oci//oci:defs.bzl", "oci_image", "oci_load", "oci_push")
load("@aspect_bazel_lib//lib:transitions.bzl", "platform_transition_filegroup")
load("@aspect_bazel_lib//lib:expand_template.bzl", "expand_template")

# gazelle:prefix github.com/tjololo/hello-go-openapi
gazelle(name = "gazelle")

buildifier(
    name = "buildifier.check",
    exclude_patterns = [
        "./.git/*",
    ],
    lint_mode = "warn",
    mode = "diff",
)

buildifier(
    name = "buildifier.fix",
    exclude_patterns = [
        "./.git/*",
    ],
    mode = "fix",
)

go_library(
    name = "hello-go-openapi_lib",
    srcs = ["main.go"],
    importpath = "github.com/tjololo/hello-go-openapi",
    visibility = ["//visibility:private"],
    deps = ["@com_github_gin_gonic_gin//:gin"],
)

go_binary(
    name = "app",
    embed = [":hello-go-openapi_lib"],
    visibility = ["//visibility:public"],
)

pkg_tar(
    name = "tar",
    srcs = [":app"],
)

oci_image(
    name = "image",
    base = "@distroless_base",
    tars = [":tar"],
    entrypoint = ["/app"],
)

# This is the target that should be released to the target platform
platform_transition_filegroup(
    name = "transitioned_image",
    srcs = [":image"],
    target_platform = select({
        "@platforms//cpu:arm64": "@rules_go//go/toolchain:linux_arm64",
        "@platforms//cpu:x86_64": "@rules_go//go/toolchain:linux_amd64",
    }),
)

oci_load(
    name = "load",
    image = ":transitioned_image",
    repo_tags = ["hello-go-api:local"],
)

expand_template(
    name = "stamped",
    out = "_stamped.tags.txt",
    template = ["0.0.0"],
    stamp_substitutions = {"0.0.0": "{{BUILD_EMBED_LABEL}}"},
)

oci_push(
    name = "push",
    image = ":transitioned_image",
    repository = "ghcr.io/tjololo/hello-go-openapi",
    remote_tags = ":stamped",
)